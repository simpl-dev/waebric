grammar ee.cyber.waebric.lexer.WaebricSimpl;

options (backtrack=true;)

scalaheader {
// import HelperMethods._
}

lexer-states(site, path, string)

Program: 'module' IdCon Definitions;

Definitions: Definition+;

option Definition: FunctionDef | Site | Import;

FunctionDef:
    "def" IdCon ("(" arguments=Formals? ")")?
	    statements=Statement*
    EndTerm;
Formals: left=IdCon ("," rest=IdCon)*;


Import: "import" module=ModuleId;
ModuleId: first=IdCon ("." rest=IdCon)*;

// File Mapping Nodes
Site:
    SiteTerm
        mappings=Mappings? 
    EndTerm;
Mappings: first=Mapping (Semicolon rest=Mapping)*;
Mapping: path=Path ":" markup=Markup;

terminal Path 
    check-all(site, path) 
    exit-state(path)
    : ( PathElement '/' )* PathElement '.' FileExt; 
fragment PathElement:( Letter | Digit | '%' )+ ; // '!'..'+' causes java heap exception
fragment FileExt:( Letter | Digit )+ ;

// These must be before IdCon
terminal SiteTerm enter-state(site, path): "site";
terminal EndTerm exit-state(site, path): "end";
terminal CommentTerm enter-state(string): "comment";

terminal PredType: ListType | RecordType | StringType;
fragment ListType: "list";
fragment RecordType: "record";
fragment StringType: "string";


// General
fragment Letter: ('a'..'z'|'A'..'Z');
fragment Digit: ('0'..'9');
fragment Hexadecimal: (Letter | Digit)+;
terminal IdCon: Letter (Letter|Digit|'-')*;
terminal NatCon: Digit+;

Markup: Designator Arguments?;  // cons(call)
Designator: IdCon Attribute*;
Arguments: "(" (first=Argument ("," rest=Argument)*)? ")";
option Argument: AttrArg | Expression;

AttrArg: IdCon "=" Expression;

option Attribute:
    IdAttr
    | ClassAttr
    | NameAttr 
    | TypeAttr 
    | WidthHeightAttr 
    | HeightAttr;

IdAttr: "#" id=IdCon;
ClassAttr: "." cl=IdCon;
NameAttr: '$' name=IdCon;
TypeAttr: ":" t=IdCon;
WidthHeightAttr: "@" width=NatCon "%" height=NatCon;
HeightAttr: "@" height=NatCon;

Expression: CatExpression | ListExpression | RecordExpression;

CatExpression: left=FieldExpression ("+" right=Expression)?;
FieldExpression: PrimitiveExpression ("." IdCon)?;
option PrimitiveExpression:
     IdCon
     | SymbolCon
     | NatCon 
     | Text;


ListExpression: "[" (first=Expression ("," rest=Expression)*)? "]";
RecordExpression: "{" (first=KeyValuePair ("," rest=KeyValuePair)*)? "}";
KeyValuePair: IdCon ":" Expression;


Statement: 
    IfStatement
    | EachStatement
    | LetStatement
    | BlockStatement
    | CommentStatement
    | EchoStatement
    | CdataStatement
    | YieldStatement
    | MarkupStatement;

IfStatement: "if" "(" Predicate ")" ifStat=Statement ("else" elseStat=Statement)?;
EachStatement: "each" "(" IdCon ":" Expression ")" Statement;  

LetStatement: "let" Assignment+ "in" Statement* EndTerm;
Assignment: VarBinding | FuncBinding;
VarBinding: IdCon "=" Expression Semicolon;
FuncBinding: func=IdCon "(" (first=IdCon ("," rest=IdCon)*)? ")" "=" Statement;

BlockStatement: "{" Statement* "}";
CommentStatement: CommentTerm StrCon Semicolon;
EchoStatement: "echo" (Expression|Embedding) Semicolon;
CdataStatement: "cdata" Expression Semicolon; 
YieldStatement: "yield" Semicolon;
MarkupStatement: Markup MarkupChain;

Predicate: left=PrimPredicate (op=PredicateOp rest=PrimPredicate)*;
terminal PredicateOp: AndOp | OrOp;
fragment AndOp: "&&";
fragment OrOp: "||";
option PrimPredicate:
    NotPredicate
    | IsAPredicate;

NotPredicate: "!" PrimPredicate;
// TODO: just Expression should be converted to ExprPredicate or something. ? WHY
// ExpPredicate: ListExpression | RecordExpression | PrimitiveExpression;
IsAPredicate: Expression ("." PredType "?")?;



MarkupChain: Semicolon | Expression Semicolon | Embedding Semicolon | Statement;

Embedding: PreText Embed TextTail;

Embed: Markup* Expression | Markup+;

TextTail: PostText | MidText Embed TextTail;


//symbol
terminal SymbolCon: "'" SymbolChar*;
fragment SymbolChar: ~( '\u0000'..'\u001F' | ' ' | ';' | ',' | '>' | '}' | ')');

//Text and TextChar related
fragment TextChar:~('\u0000'..'\u001F' | '&' | '"' | '<' | '\u0080'..'\uFFFF' ) |
 '\n' | '\r' | '\t' | EscQuote | Amp | CharRef | EntRef ;
fragment EscQuote:'\\\\' | '\\"';
fragment Amp:'\&' ~('#' | '0'..'9' | 'a'..'z' | 'A'..'Z' | '_' | ':')+;
fragment CharRef:'&#' Digit+ ';' | '&#x' Hexadecimal ';' ;
fragment EntRef:'&' ( Letter | '_' | ':' ) ( Letter | Digit | '.' | '-' | '_' | ':')* ';' ;
// TODO: add check that this terminal must not be matched when lexer
// state contains string.
terminal Text: '"' TextChar* '"';

terminal PreText: '"' TextChar* '<';
terminal PostText: '>' TextChar* '"';
terminal MidText: '>' TextChar* '<';

//StrCon and StrChar
//As this will be processed after Text, all patterns that match
//both, were already considered as Text. "comment" tag only
//uses StrCon and this must be considered.

fragment Newline: '\\n';
fragment Tab: '\\t';
fragment Quote: '\\"';
fragment Backslash: '\\\\';
fragment Decimal: '\\\\' 'a:' Digit 'b:' Digit 'c:' Digit;
fragment StrChar: ~('\u0000'..'\u001F' | '"' | '\\') | Newline | Tab | Quote | Backslash | Decimal;
terminal StrCon check-any(string) exit-state(string): '"' StrChar* '"';


terminal Semicolon enter-state(path): ";";

// whitespaces and comments
fragment ML_COMMENT: '/*' (~'*' | '*' ~'/')* '*/';
fragment SL_COMMENT: '//' ~('\n'|'\r')*;
hidden terminal WS: (' '|'\t'|'\r'|'\n'|SL_COMMENT|ML_COMMENT)+;
